plugins {
    id 'java'
    id 'jvm-test-suite'
    id 'org.springframework.boot' version '3.0.2'
    id "io.freefair.lombok" version "6.6.1"
    id 'de.undercouch.download' version '4.1.1'
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

group 'bluegecko.co.uk'
version '0.0.1-SNAPSHOT'
description = "Marine Sample App"
ext.moduleName = "uk.co.bluegecko.marine.sample"

repositories {
    mavenCentral()
}

bootRun {
    mainClass.set 'uk.co.bluegecko.marine.sample.SampleApplication'
    jvmArgs = ['-javaagent:build/otel/opentelemetry-javaagent.jar']
}

bootJar {
    dependsOn("downloadAgent")
}

dependencies {
    implementation 'io.opentelemetry:opentelemetry-api'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.locationtech.spatial4j:spatial4j:0.8'
    implementation 'tech.uom.lib:uom-lib-jackson:2.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    runtimeOnly 'com.h2database:h2'
}

testing {
    suites {
        configureEach {
            useJUnitJupiter()
            dependencies {
                implementation 'org.springframework.boot:spring-boot-starter-test'
                implementation 'org.assertj:assertj-core'
                implementation 'org.mockito:mockito-core'
            }
        }

        test {
        }

        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project()
                compileOnly 'org.springframework:spring-web'
                compileOnly 'org.springframework.data:spring-data-jpa'
            }
        }
    }
}

tasks.named('check') {
    dependsOn testing.suites.integrationTest
}

task downloadAgent(type: Download) {
    src "https://github.com/open-telemetry/opentelemetry-java-instrumentation/releases/latest/download/opentelemetry-javaagent.jar"
    dest project.buildDir.toString() + "/otel/opentelemetry-javaagent.jar"
    overwrite true
    onlyIfModified true
}